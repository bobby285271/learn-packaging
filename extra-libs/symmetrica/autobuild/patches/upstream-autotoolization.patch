Description: upstream autotoolization
 Attempt to autotoolize Symmetrica source; meant
 to be submitted to the upstream maintainer.
Origin: debian
Author: Jerome Benoit <calculus@rezozer.net>
Last-Update: 2014-09-13

--- /dev/null
+++ b/configure.ac
@@ -0,0 +1,46 @@
+dnl                                               -*- Autoconf -*-
+dnl Process this file with autoconf to produce a configure script.
+AC_PREREQ([2.69])
+AC_INIT([SYMMETRICA],[2.0],
+	[symmetrica@symmetrica.de],[symmetrica],
+	[http://www.algorithm.uni-bayreuth.de/en/research/SYMMETRICA/])
+AC_CONFIG_MACRO_DIR([m4])
+AC_CONFIG_AUX_DIR([build-aux])
+AC_CONFIG_SRCDIR([galois.c])
+AC_CONFIG_HEADERS([symmetrica_config.h])
+AM_INIT_AUTOMAKE([foreign])
+AM_MAINTAINER_MODE([])
+
+dnl LibTool setup.
+LT_INIT
+
+dnl check linker script support
+gl_LD_VERSION_SCRIPT
+
+dnl Checks for programs.
+AC_PROG_CC_C99
+
+dnl Checks for libraries.
+LT_LIB_M
+
+dnl Checks for header files.
+AC_CHECK_HEADERS([malloc.h memory.h stdlib.h string.h sys/param.h unistd.h])
+
+dnl Checks for typedefs, structures, and compiler characteristics.
+AC_TYPE_SIZE_T
+
+dnl Checks for library functions.
+AC_FUNC_ERROR_AT_LINE
+AC_FUNC_MALLOC
+AC_FUNC_REALLOC
+AC_CHECK_FUNCS([memset])
+
+AC_CONFIG_FILES([
+	sym_test.sh
+	Makefile
+	],[
+	chmod a+x sym_test.sh
+	])
+AC_OUTPUT
+
+dnl eof
--- /dev/null
+++ b/Makefile.am
@@ -0,0 +1,162 @@
+## Makefile.am -- AutoMake master file
+
+ACLOCAL_AMFLAGS = -I m4
+
+AUTOMAKE_OPTIONS = \
+	foreign \
+	dist-xz \
+	dist-bzip2
+
+AM_DISTCHECK_CONFIGURE_FLAGS ?= \
+	--disable-maintainer-mode
+
+## Library versioning (C:R:A == current:revision:age)
+LIBSYMMETRICA_LT_VERSION = 2:0:0
+
+#SUBDIRS =
+
+include_HEADERS = \
+	symmetrica.h
+
+pkginclude_HEADERS = \
+	def.h \
+	macro.h
+
+lib_LTLIBRARIES = libsymmetrica.la
+
+libsymmetrica_la_LD_VERSION_SCRIPT =
+if HAVE_LD_VERSION_SCRIPT
+libsymmetrica_la_LD_VERSION_SCRIPT += -Wl,--version-script=$(top_srcdir)/symmetrica.map
+endif
+
+check_SCRIPTS = sym_test.sh
+
+check_PROGRAMS = test
+
+TESTS = $(check_SCRIPTS)
+TESTS_ENVIRONMENT = VERBOSE=1
+
+noinst_HEADERS = symmetrica_config.h
+
+libsymmetrica_la_CPPFLAGS = \
+	-DFAST \
+	-DALLTRUE
+
+libsymmetrica_la_CFLAGS = \
+	-Wno-unused-result
+
+libsymmetrica_la_SOURCES = \
+	bar.c \
+	bi.c \
+	boe.c \
+	bruch.c \
+	classical.c \
+	de.c \
+	di.c \
+	ff.c \
+	galois.c \
+	ga.c \
+	gra.c \
+	hash.c \
+	hiccup.c \
+	io.c \
+	ko.c \
+	list.c \
+	lo.c \
+	ma.c \
+	mee.c \
+	mem.c \
+	mes.c \
+	mhe.c \
+	mhh.c \
+	mhm.c \
+	mhp.c \
+	mhs.c \
+	mmm.c \
+	mms.c \
+	mod_dg_sbd.c \
+	mo.c \
+	mpp.c \
+	mps.c \
+	mse.c \
+	msh.c \
+	msm.c \
+	mss.c \
+	muir.c \
+	na.c \
+	nb.c \
+	nc.c \
+	nu.c \
+	part.c \
+	pee.c \
+	peh.c \
+	pem.c \
+	perm.c \
+	pes.c \
+	phe.c \
+	phh.c \
+	phm.c \
+	phs.c \
+	plet.c \
+	pme.c \
+	pmh.c \
+	poly.c \
+	ppe.c \
+	pph.c \
+	ppm.c \
+	ppp.c \
+	pps.c \
+	pr.c \
+	pse.c \
+	psh.c \
+	psm.c \
+	pss.c \
+	rest.c \
+	rh.c \
+	sab.c \
+	sb.c \
+	sc.c \
+	sr.c \
+	ta.c \
+	teh.c \
+	tem.c \
+	tep.c \
+	tes.c \
+	the.c \
+	thm.c \
+	thp.c \
+	ths.c \
+	tme.c \
+	tmh.c \
+	tmp.c \
+	tms.c \
+	tpe.c \
+	tph.c \
+	tpm.c \
+	tps.c \
+	tse.c \
+	tsh.c \
+	tsm.c \
+	tsp.c \
+	vc.c \
+	zo.c \
+	zykelind.c \
+	zyk.c
+
+libsymmetrica_la_LDFLAGS = \
+	-version-info $(LIBSYMMETRICA_LT_VERSION) \
+	$(libsymmetrica_la_LD_VERSION_SCRIPT)
+
+libsymmetrica_la_LIBADD = \
+	$(LIBM)
+
+test_CPPFLAGS = \
+	$(libsymmetrica_la_CPPFLAGS)
+
+test_SOURCES = \
+	test.c
+
+test_LDADD = \
+	libsymmetrica.la
+
+## eof
--- a/makefile
+++ /dev/null
@@ -1,4 +0,0 @@
-.c.o:
-	cc -c -O2 -DFAST -DALLTRUE $<
-test: test.c bar.o bi.o boe.o bruch.o classical.o de.o di.o ff.o galois.o ga.o gra.o hash.o hiccup.o io.o ko.o list.o lo.o ma.o mee.o mem.o mes.o mhe.o mhh.o mhm.o mhp.o mhs.o mmm.o mms.o mod_dg_sbd.o mo.o mpp.o mps.o mse.o msh.o msm.o mss.o muir.o na.o nb.o nc.o nu.o part.o pee.o peh.o pem.o perm.o pes.o phe.o phh.o phm.o phs.o plet.o pme.o pmh.o poly.o ppe.o pph.o ppm.o ppp.o pps.o pr.o pse.o psh.o psm.o pss.o rest.o rh.o sab.o sb.o sc.o sr.o ta.o teh.o tem.o tep.o tes.o the.o thm.o thp.o ths.o tme.o tmh.o tmp.o tms.o tpe.o tph.o tpm.o tps.o tse.o tsh.o tsm.o tsp.o vc.o zo.o zykelind.o zyk.o
-	gcc -DALLTRUE -DFAST test.c bar.o bi.o boe.o bruch.o classical.o de.o di.o ff.o galois.o ga.o gra.o hash.o hiccup.o io.o ko.o list.o lo.o ma.o mee.o mem.o mes.o mhe.o mhh.o mhm.o mhp.o mhs.o mmm.o mms.o mod_dg_sbd.o mo.o mpp.o mps.o mse.o msh.o msm.o mss.o muir.o na.o nb.o nc.o nu.o part.o pee.o peh.o pem.o perm.o pes.o phe.o phh.o phm.o phs.o plet.o pme.o pmh.o poly.o ppe.o pph.o ppm.o ppp.o pps.o pr.o pse.o psh.o psm.o pss.o rest.o rh.o sab.o sb.o sc.o sr.o ta.o teh.o tem.o tep.o tes.o the.o thm.o thp.o ths.o tme.o tmh.o tmp.o tms.o tpe.o tph.o tpm.o tps.o tse.o tsh.o tsm.o tsp.o vc.o zo.o zykelind.o zyk.o -o test -lm
--- /dev/null
+++ b/m4/ld-version-script.m4
@@ -0,0 +1,53 @@
+# ld-version-script.m4 serial 3
+dnl Copyright (C) 2008-2014 Free Software Foundation, Inc.
+dnl This file is free software; the Free Software Foundation
+dnl gives unlimited permission to copy and/or distribute it,
+dnl with or without modifications, as long as this notice is preserved.
+
+dnl From Simon Josefsson
+
+# FIXME: The test below returns a false positive for mingw
+# cross-compiles, 'local:' statements does not reduce number of
+# exported symbols in a DLL.  Use --disable-ld-version-script to work
+# around the problem.
+
+# gl_LD_VERSION_SCRIPT
+# --------------------
+# Check if LD supports linker scripts, and define automake conditional
+# HAVE_LD_VERSION_SCRIPT if so.
+AC_DEFUN([gl_LD_VERSION_SCRIPT],
+[
+  AC_ARG_ENABLE([ld-version-script],
+    AS_HELP_STRING([--enable-ld-version-script],
+      [enable linker version script (default is enabled when possible)]),
+      [have_ld_version_script=$enableval], [])
+  if test -z "$have_ld_version_script"; then
+    AC_MSG_CHECKING([if LD -Wl,--version-script works])
+    save_LDFLAGS="$LDFLAGS"
+    LDFLAGS="$LDFLAGS -Wl,--version-script=conftest.map"
+    cat > conftest.map <<EOF
+foo
+EOF
+    AC_LINK_IFELSE([AC_LANG_PROGRAM([], [])],
+                   [accepts_syntax_errors=yes], [accepts_syntax_errors=no])
+    if test "$accepts_syntax_errors" = no; then
+      cat > conftest.map <<EOF
+VERS_1 {
+        global: sym;
+};
+
+VERS_2 {
+        global: sym;
+} VERS_1;
+EOF
+      AC_LINK_IFELSE([AC_LANG_PROGRAM([], [])],
+                     [have_ld_version_script=yes], [have_ld_version_script=no])
+    else
+      have_ld_version_script=no
+    fi
+    rm -f conftest.map
+    LDFLAGS="$save_LDFLAGS"
+    AC_MSG_RESULT($have_ld_version_script)
+  fi
+  AM_CONDITIONAL(HAVE_LD_VERSION_SCRIPT, test "$have_ld_version_script" = "yes")
+])
--- /dev/null
+++ b/sym_test.sh.in
@@ -0,0 +1,8 @@
+#!/bin/sh
+##
+TESTEXEC=@abs_top_builddir@/test
+[ -x $TESTEXEC ] || exit 2
+[ $(echo 11 | $TESTEXEC 2> /dev/null) = 39916800 ] || exit 1
+exit 0
+##
+## eos
